group 'com.uni-tuebingen.de.it.eager.ReportTable'
version '1.92.29'

apply plugin: 'java'
apply plugin: 'eclipse-wtp'

eclipse.classpath.file {
    withXml { xml ->
        def node = xml.asNode()
        node.remove( node.find { it.@path == 'org.eclipse.jst.j2ee.internal.web.container' } )
        node.appendNode( 'classpathentry', [ kind: 'con', path: 'org.eclipse.jst.j2ee.internal.web.container', exported: 'true'])
    }
}


buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
    }
}


allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.jfrog.bintray'
}

sourceCompatibility = 1.8


sourceSets {
    main {
        java {
            srcDir 'src'
        }
        resources {
            srcDir 'resources'
        }
    }
}


dependencies {
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.9'
    compile 'com.uni-tuebingen.de.it.eager:EAGER-lib:1.92+'
}

jar {
    manifest {
        attributes("Implementation-Title": "ReportTable",
                "Implementation-Version": version, "main-Class": "main.ReportTable")
    }
    doFirst {
        from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.uni-tuebingen.de.it.eager'
            artifactId 'ReportTable'
        }
    }
}


bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['MyPublication']
    publish = true
    override = true
    pkg {
        repo = 'EAGER'
        name = 'ReportTable'
        licenses = ['GPL-3.0']
        vcsUrl = "https://github.com/apeltzer/ReportTable"
        version {
            name = project.version
            desc = 'The Report Generation Engine for the EAGER Pipeline'
            released  = new Date()
            vcsTag = project.version
            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin']
        }

    }
}
